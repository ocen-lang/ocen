//
// This is meant to be run for every single LSP request, it takes in a location in a file
// and returns the requested information (type/definition/etc)
//

import std::buffer::Buffer
import std::span::{ Span, Location }
import std::vector::Vector
import std::value::Value
import std::{ panic, exit }
import std::fs
import std::json
import std::signal::{ set_signal_handler, Signal }
import std::setjmp::{ ErrorContext }

import @lexer::Lexer
import @parser::Parser
import @passes::{ run_typecheck_passes }
import @ast::nodes::*
import @ast::program::*
import @ast::scopes::*
import @types::*
import @passes::visitor::Visitor

import .finder::{ Finder }
import .utils::{ this, verbose }

enum CommandType {
    Hover
    GoToDefinition
    GoToType
    None
    DocumentSymbols
    Completions
    References
    Renames
    SignatureHelp
    Validate
}

def typecheck_and_log_errors(program: &Program, path: str) {
    run_typecheck_passes(program)
    if program.errors.size == 0 {
        if verbose then println("type-checked program successfully")
        return
    }

    for err in program.errors.iter() {
        let start = err.span1.start
        let end = err.span1.end
        if verbose then println("[-] ERROR: %s:%d:%d - %d:%d :: %s", start.filename, start.line, start.col, end.line, end.col, err.msg1)
    }
}

def handle_validate(program: &Program, path: str) {
    typecheck_and_log_errors(program, path)
    for err in program.errors.iter() {
        if not err.span1.start.filename? continue
        if not (err.span1.start.filename == path) continue

        let err_value = utils::gen_error_json(err)
        println(`{err_value.dbg()}`)
    }


    return  // Look below

    /// TODO: The following code "works", but current way of handling this in server
    //        clears all hints when document is changed and validate is called, which
    //        causes the hints to appear glitchy when user is typing. Need some way to
    //        keep the old hints around till we have a next successful validate call?
    //  For now, just disable this code and only show errors
    let visitor = Visitor(
        node_fn: |node| => match node.type {
            VarDeclaration => {
                let msg = utils::gen_inlay_hint(node.u.var_decl, path)
                if msg? then println(`{msg.dbg()}`)
            }
            CreateClosure => {
                for param in node.u.closure.params.iter() {
                    let msg = utils::gen_inlay_hint(param, path)
                    if msg? then println(`{msg.dbg()}`)
                }
            }
            else => {}
        }
    )
    for ns in program.iter_namespaces() {
        if ns.path.eq(path) {
            for func in ns.functions.iter() {
                if func.body? {
                    visitor.visit_po(func.body)
                }
            }
        }
    }
}


def handle_location_command(program: &Program, type: CommandType, loc: Location) {
    typecheck_and_log_errors(program, loc.filename)

    if verbose then println(`[+] Looking for location: {loc}`)
    let finder = Finder::make(cmd: type, loc)

    if not finder.find(program) {
        if verbose then println("[-] No result found")
        return
    }

    let resp = match type {
        Hover => utils::gen_hover_string_with_docs(finder.found_sym)
        GoToDefinition => {
            let usage = finder.found_sym
            let jump_span = match usage.type {
                Namespace => usage.u.ns.span
                else => usage.span
            }
            yield utils::gen_span_json_with_filename(jump_span, loc)
        }
        GoToType => {
            let typ = utils::get_symbol_typedef(finder.found_sym)
            if typ? and not typ.can_have_methods() and typ.base == Pointer {
                typ = typ.u.ptr
            }
            if not typ? return
            yield utils::gen_span_json_with_filename(typ.span, loc)
        }
        Completions => utils::gen_completions_json(&finder)
        References => utils::gen_references_json(finder.found_sym, loc)
        Renames => utils::gen_renames_json(finder.found_sym, loc)
        SignatureHelp => utils::gen_signature_help(finder.call, finder.active_param)
        else => panic("Unhandled command type")
    }
    if not resp? return
    let resp_text = json::serialize(resp)
    println(`{resp_text}`)
}

def handle_document_symbols(program: &Program, path: str) {
    typecheck_and_log_errors(program, path)

    let doc_ns: &Namespace = null
    for ns in program.iter_namespaces() {
        let ns_filename = ns.span.start.filename
        if ns_filename? and ns_filename.eq(path) {
            doc_ns = ns
            break
        }
    }

    if not doc_ns? {
        if verbose then println(f"No namespace found for path: {path}")
        // Empty array so we don't crash the LSP client
        println("[]")
        return
    }

    if verbose println(f"Got ns: {doc_ns.sym.name}")

    let resp = utils::gen_namespace_json(doc_ns)
    if not resp? return

    let children = resp.get("children")
    let resp_text = json::serialize(children)
    println(`{resp_text}`)
}

def shift_args(argc: &i32, argv: &&str): str {
    if *argc == 0 {
        println(f"Expected argument but got none")
        exit(1)
    }
    let arg = (*argv)[0]
    (*argv) = (*argv) + 1
    (*argc) = (*argc) - 1
    return arg
}

def lsp_usage(code: i32, full: bool) {
    println("Symbol:")
    println("   ./ocen [--help] [compile-options] <file>")
    println("   ./ocen lsp [--help] [lsp-options] <file>")
    if not full then exit(code)

    println("--------------------------------------------------------")
    println("LSP Options:")
    println("    --show-path path          Display this file path instead of actual one")
    println("    -h <line> <col>           Type hints for the given location")
    println("    -d <line> <col>           Find definition for the given location")
    println("    -t <line> <col>           Find type for the given location")
    println("    -c <line> <col>           Completions for the given location")
    println("    -r <line> <col>           References for the given location")
    println("    -s <line> <col>           Signature Help for the given location")
    println("    --renames <line> <col>    Find all references for the given location")
    println("    -v                        Verbose output")
    println("    --doc-symbols             List all symbols in the file")
    println("    --validate                List all errors in the file")
    exit(code)
}

let global_err_ctx: ErrorContext

def signal_handler(sig: i32) {
    if verbose then println(`Received signal {sig as Signal}, exiting`)
    global_err_ctx.jump_back(1)
}

def lsp_main(argc: i32, argv: &str) {
    shift_args(&argc, &argv) // skip the first argument

    let show_path: str = null
    let file_path: str = null
    let line: u32 = 0
    let col: u32 = 0
    let cmd_type = CommandType::None

    while argc > 0 {
        let arg = shift_args(&argc, &argv)
        match arg {
            "--help" => lsp_usage(0, true)
            "-h" | "-d" | "-t" | "-c" | "-r" | "-s" | "--refs" | "--renames" => {
                cmd_type = match arg {
                    "-h" => Hover
                    "-d" => GoToDefinition
                    "-t" => GoToType
                    "-c" => Completions
                    "-s" => SignatureHelp
                    "-r" | "--refs" => References
                    "--renames" => Renames
                    else => panic("Invalid command type")
                }
                line = shift_args(&argc, &argv).to_u32()
                col = shift_args(&argc, &argv).to_u32()
            }
            "--validate" => cmd_type = Validate
            "--doc-symbols" => cmd_type = DocumentSymbols
            "-v" => verbose = true
            "--show-path" => show_path = shift_args(&argc, &argv)
            else => {
                if file_path? {
                    println(`Too many arguments, got: {arg}`)
                    lsp_usage(1, false)
                }
                file_path = arg
            }
        }
    }
    if not file_path? {
        println("No file path provided")
        lsp_usage(1, false)
    }
    file_path = fs::realpath(file_path)

    if cmd_type == CommandType::None {
        println("No command type provided")
        lsp_usage(1, false)
    }

    if not show_path? {
        show_path = file_path
    } else {
        show_path = fs::realpath(show_path)
    }

    set_signal_handler(SIGSEGV, signal_handler)
    set_signal_handler(SIGILL, signal_handler)
    set_signal_handler(SIGFPE, signal_handler)

    // Load the program
    let program = Program::new()

    if global_err_ctx.set_jump_point() > 0 {
        // Do nothing here, since LSP is sensitive to unnecessary output
        exit(1)
    }

    program.setup_library_paths()
    // Always try to load stdlib for LSP
    program.include_stdlib = true

    let contents = fs::read_file(file_path).str()
    // For references and renames we want to look at all files in the workspace
    let include_workspace_main = match cmd_type {
        References | Renames => true
        else => false
    }
    Parser::parse_toplevel(program, show_path, contents, include_workspace_main)

    match cmd_type {
        DocumentSymbols => handle_document_symbols(program, show_path)
        Validate => handle_validate(program, show_path)
        else => {
            let loc = Location(show_path, line, col, 0)
            handle_location_command(program, cmd_type, loc)
        }
    }
}