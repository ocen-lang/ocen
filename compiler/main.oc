//* Entry point for the compiler

import std::fs
import std::libc::{ exit, calloc, system }
import std::vector::Vector

import .ast::program::Program
import .parser::Parser
import .passes::{ run_typecheck_passes, run_codegen_passes }
import .docgen::generate_doc_json
import .lsp::lsp_main

def shift_args(argc: &i32, argv: &&str, where: str = "here"): str {
    if *argc == 0 {
        println(f"Expected argument but got none")
        exit(1)
    }
    let arg = (*argv)[0]
    (*argv) = (*argv) + 1
    (*argc) = (*argc) - 1
    return arg
}

def usage(code: i32, full: bool) {
    println("Usage:")
    println("   ./ocen [--help] [compile-options] <file>")
    println("   ./ocen lsp [--help] [lsp-options] <file>")
    if not full then exit(code)

    println("--------------------------------------------------------")
    println("Compile Options:")
    println("    -o path        Output executable (default: ./out)")
    println("    -c path        Output C code (default: {out}.c)")
    println("    --no-stdlid    Don't include the standard library")
    println("    -e0            Minimal one-line errors")
    println("    -e1            Error messages with source code (default)")
    println("    -e2            Error messages with source / hints")
    println("    -s             Silent mode (no debug output)")
    println("    -n             Don't compile C code (default: false)")
    println("    --no-dce       Don't perform dead code elimination")
    println("    -d             Emit debug information (default: false)")
    println("    -l path        Directory to search for libraries (can be used multiple times)")
    println("    --docs path    Output documentation JSON (default: none)")
    println("    --cflags flags Additional C flags (can be used multiple times)")
    println("    -h             Display this information")
    exit(code)
}

let exec_path: str = "./out"
let c_path: str = null
let filename: str = null
let compile_c: bool = true
let silent: bool = false
let debug: bool = false
let error_level: u32 = 2
let docs_path: str = null
let include_stdlib: bool = true
let extra_c_flags: &Vector<str>

def save_and_compile_code(program: &Program, code: str) {
    if not c_path? {
        c_path = `{exec_path}.c`
    }

    fs::write_file_str(c_path, code)

    if not compile_c then return

    let cmdbuf = calloc(1, 1024) as str
    let c_compiler = std::libc::getenv("CC")
    if not c_compiler? then c_compiler = "gcc"
    cmdbuf.concat(`{c_compiler} -o {exec_path} {c_path}`)
    for flag : program.c_flags.iter() {
        cmdbuf.concat(" ")
        cmdbuf.concat(flag)
    }
    for flag : extra_c_flags.iter() {
        cmdbuf.concat(" ")
        cmdbuf.concat(flag)
    }
    if debug {
        cmdbuf.concat(" -ggdb3")
    }

    if not silent {
        println("[+] %s", cmdbuf)
    }
    let exit_code = system(cmdbuf)
    if exit_code != 0 {
        println("[-] Compilation failed")
        exit(1)
    }
}

def parse_args(argc: i32, argv: &str, program: &Program) {
    extra_c_flags = Vector<str>::new()

    while argc > 0 {
        let arg = shift_args(&argc, &argv)
        match arg {
            "--help" => usage(code: 0, true)
            "-s" => silent = true
            "-d" => debug = true
            "-n" => {
                compile_c = false
                program.keep_all_code = true
            }
            "--no-dce" => program.keep_all_code = true
            "-o" => exec_path = shift_args(&argc, &argv)
            "-c" => c_path = shift_args(&argc, &argv)
            "-l" => program.library_paths.push(shift_args(&argc, &argv))
            "-e0" => error_level = 0
            "-e1" => error_level = 1
            "-e2" => error_level = 2
            "--docs" => {
                docs_path = shift_args(&argc, &argv)
                program.check_doc_links = true
            }
            "--no-stdlib" => include_stdlib = false
            "--cflags" | "-cf" => extra_c_flags.push(shift_args(&argc, &argv))
            else => {
                if arg[0] == '-' {
                    println("Unknown option: %s", arg)
                    usage(1, true)
                } else if not filename? {
                    filename = arg
                } else {
                    println("Unknown option/argument: '%s'", arg)
                    usage(code: 1, true)
                }
            }
        }
    }

    if not filename? {
        println("No file specified")
        usage(code: 1, false)
    }
}

def main(argc: i32, argv: &str) {
    shift_args(&argc, &argv)
    if argc > 1 and argv[0].eq("lsp") {
        return lsp_main(argc, argv)
    }

    let program = Program::new()

    program.library_paths.push("")
    let ocen_root = std::libc::getenv("OCEN_ROOT")
    if ocen_root? and ocen_root.len() > 0 {
        program.library_paths.push(ocen_root)
    }

    parse_args(argc, argv, program)

    program.error_level = error_level
    program.gen_debug_info = debug
    Parser::parse_toplevel(program, filename, include_stdlib)

    if not program.errors.is_empty() then program.exit_with_errors()

    run_typecheck_passes(program)

    if not program.errors.is_empty() then program.exit_with_errors()

    if docs_path? {
        generate_doc_json(program, docs_path)

    } else {
        let code = run_codegen_passes(program)

        if not program.errors.is_empty() then program.exit_with_errors()

        save_and_compile_code(program, code)
    }
}
