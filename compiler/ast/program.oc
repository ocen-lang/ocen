//* Contains the Program/Namespace types

import std::vector::Vector
import std::span::Span
import std::map::Map
import std::mem
import std::setjmp::{ ErrorContext }
import std::sv::{ SV }
import @ast::scopes::{ Symbol, Scope }
import @ast::nodes::{ AST, Function, Structure, Enum, Variable }
import @ast::operators::{ OperatorOverload }
import @errors::Error
import @types::{ Type, BaseType }
import @parser::Parser
import @lexer::Lexer
import @errors::{ display_error_messages }
import @passes

struct Namespace {
    parent: &Namespace

    functions: &Vector<&Function>
    structs: &Vector<&Structure>
    enums: &Vector<&Enum>
    constants: &Vector<&AST>
    variables: &Vector<&AST>
    imports: &Vector<&AST>
    typedefs: &Map<str, &Type>
    namespaces: &Map<str, &Namespace>
    exported_symbols: &Map<str, &Symbol>

    sym: &Symbol
    scope: &Scope
    path: str
    span: Span

    // Does this namespace correspond to an actual file with source code? Or is it just an
    // intermediate namespace for a directory?
    is_a_file: bool

    // This is used for any namespaces that need to be added to the scope by default. For instance,
    // the standard library, or user-defined namespaces with the `namespace` keyword.
    always_add_to_scope: bool

    //! Does this namespace correspond to a directory with an implicitly loaded `mod.oc` file?
    //! If this is true, the `path` for this namespace is the directory path, but the actual namespace
    //! contents are from the `mod.oc` file. This works out easier when we're trying to import
    is_dir_with_mod: bool

    internal_project_root: &Namespace

    //! For use during parsing, we only resolve imports after parsing the namespace
    unhandled_imports: &Vector<&AST>
}

def Namespace::new(parent: &Namespace, path: str): &Namespace {
    let ns = mem::alloc<Namespace>()
    ns.parent = parent
    let parent_scope = if parent? then parent.scope else null
    let parent_root = if parent? then parent.internal_project_root else null

    ns.scope = null // Initialized post-parser
    ns.functions = Vector<&Function>::new()
    ns.structs = Vector<&Structure>::new()
    ns.enums = Vector<&Enum>::new()
    ns.constants = Vector<&AST>::new()
    ns.variables = Vector<&AST>::new()
    ns.typedefs = Map<str, &Type>::new()
    ns.namespaces = Map<str, &Namespace>::new()
    ns.imports = Vector<&AST>::new()
    ns.exported_symbols = Map<str, &Symbol>::new()
    ns.unhandled_imports = Vector<&AST>::new()
    ns.path = path
    ns.is_a_file = false
    ns.internal_project_root = parent_root
    return ns
}

//! Dumps out the contents of the namespace (not recursive) for debug
def Namespace::dump(&this) {
    println(f"============= NS: {.sym.display} {.path} ==============")
    println("   Functions:")
    for func in .functions.iter() {
        println(f"     - {func.sym.display}")
    }
    println(f"   Structs:")
    for s in .structs.iter() {
        println(f"     - {s.sym.display}")
    }
    println(f"   Enums:")
    for v in .enums.iter() {
        println(f"     - {v.sym.display}")
    }
    println(f"   Constants:")
    for c in .constants.iter() {
        println(f"     - {c.u.var_decl.sym.display}")
    }
    println(f"   Variables:")
    for v in .variables.iter() {
        println(f"     - {v.u.var_decl.sym.display}")
    }
    println(f"   Typedefs:")
    for td in .typedefs.iter() {
        println(f"     - {td.key}")
    }
    println(f"=======================================================")
}

def Namespace::find_importable_symbol(&this, name: str): &Symbol {
    let item = .namespaces.get(name, null)
    if item? then return item.sym

    for node in .constants.iter() {
        let var = node.u.var_decl
        if var.sym.name.eq(name) return var.sym
    }

    for node in .variables.iter() {
        let var = node.u.var_decl
        if var.sym.name.eq(name) return var.sym
    }

    for func in .functions.iter() {
        if func.kind == Method then continue
        if func.sym.name.eq(name) return func.sym
    }

    for struc in .structs.iter() {
        if struc.sym.name.eq(name) return struc.sym
    }

    for enom in .enums.iter() {
        if enom.sym.name.eq(name) return enom.sym
    }

    if .exported_symbols.contains(name) {
        return .exported_symbols.at(name)
    }

    let td = .typedefs.get(name, null)
    if td? then return td.sym

    return null
}

// FIXME: There's logic here that is already in Parser, need to refactor these
def Namespace::get_project_root(&this, span: Span, program_for_errors: &Program): &Namespace {
    if not .internal_project_root? {
        program_for_errors.error(Error::new_note(
            span, "No project root found, can't use a Project Namespace import",
            "A project root is a top-level directory containing a `main.oc` file."
        ))
    }
    return .internal_project_root
}

struct Program {
    global: &Namespace

    //* Symbols in topologically sorted order. Filled by {{passes::reorder_symbols}}
    ordered_symbols: &Vector<&Symbol>

    c_includes: &Vector<str>
    c_flags: &Vector<str>

    operator_overloads: &Map<OperatorOverload, &Function>

    //* Mapping from file name to file contents
    c_embeds: &Map<str, str>
    sources: &Map<str, str>

    library_paths: &Vector<str>

    errors: &Vector<&Error>
    error_level: u32

    cached_symbols: CachedSymbols
    did_cache_symbols: bool
    err_jmp_stack: &Vector<ErrorContext>

    explicit_alive_symbols: &Vector<&Symbol>

    closure_types: &Vector<&Type>
    closures: &Vector<&Function> // FIXME: what about closed args?

    // Configs
    check_doc_links: bool
    gen_debug_info: bool
    keep_all_code: bool
    include_stdlib: bool

    // State
    uid: u32  // For generating unique IDs
}

def Program::new(): &Program {
    let prog = mem::alloc<Program>()
    prog.global = Namespace::new(null, "")
    prog.ordered_symbols = Vector<&Symbol>::new()
    prog.global.sym = Symbol::new(
        Namespace,
        ns: prog.global,
        name: "",
        display: "",
        full_name: "",
        Span::default()
    )
    prog.global.sym.u.ns = prog.global
    prog.error_level = 1
    prog.errors = Vector<&Error>::new()
    prog.c_includes = Vector<str>::new()
    prog.c_flags = Vector<str>::new()
    prog.c_embeds = Map<str, str>::new()
    prog.sources = Map<str, str>::new()
    prog.library_paths = Vector<str>::new()
    prog.operator_overloads = Map<OperatorOverload, &Function>::new()
    prog.err_jmp_stack = Vector<ErrorContext>::new()
    prog.explicit_alive_symbols = Vector<&Symbol>::new()
    prog.closure_types = Vector<&Type>::new()
    prog.closures = Vector<&Function>::new()
    prog.uid = 0
    return prog
}

def Program::add_error_context(&this): &ErrorContext {
    .err_jmp_stack.resize(.err_jmp_stack.size + 1)
    .err_jmp_stack.size += 1
    return .err_jmp_stack.back_ptr()
}

def Program::get_error_context(&this): &ErrorContext {
    return .err_jmp_stack.back_ptr()
}

def Program::pop_error_context(&this) {
    .err_jmp_stack.size -= 1
}

def Program::setup_library_paths(&this) {
    .library_paths.push("")
    let ocen_root = std::libc::getenv("OCEN_ROOT")
    if ocen_root? and ocen_root.len() > 0 {
        .library_paths.push(ocen_root)
    }

    let ocen_lib = std::libc::getenv("OCEN_LIB")
    if ocen_lib? {
        let oc_sv = SV::from_str(ocen_lib)
        for part in oc_sv.split_str(":") {
            .library_paths.push(part.copy_data_to_cstr())
        }
    }
}

def Program::exit_with_errors_if_any(&this) {
    if not .errors.is_empty() {
        display_error_messages(.errors, .error_level)
        std::exit(1)
    }
}

def Program::get_source_text(&this, span: Span): str {
    let start = span.start
    let end = span.end

    let contents = .sources.get(start.filename, null) as str
    if not contents? return null

    let len = end.index - start.index
    return contents.substring(start.index, len)
}

def Program::error(&this, err: &Error): &Error {
    .errors.push(err)
    return err
}

// FIXME: This logic looks too similar to Type::eq() for 2 functions, however we want to
//        try not to create a new dummy Type object before we call this function to avoid
//        memory allocations. There is probably a way to refactor this to avoid the duplication.
def Program::find_closure_type(&this, params: &Vector<&Variable>, ret: &Type): &Type {

    // FIXME: Can we do a non-linear search here?
    for c in .closure_types.iter() {
        let clos = c.u.func
        if clos.params.size != params.size then continue
        if not clos.return_type.eq(ret) then continue
        let all_match = true
        for let i = 0; i < params.size; i += 1 {
            if not clos.params.at(i).type.eq(params.at(i).type) {
                all_match = false
                break
            }
        }
        if not all_match then continue
        // FIXME: Variadic? Other attributes?
        return c
    }
    return null
}

def get_deep_copy<T>(
    program: &Program,
    old: &T,
    ns: &Namespace,
    parse_fn: fn(&Parser): &T
): &T {
    // FIXME: This is a hack to get around the fact that we can't deep-copy
    // objects yet. We're just going to get the source code of the function
    // and re-parse it. This is obviously not ideal.

    let obj_text = program.get_source_text(old.span)
    if not obj_text? return null

    let lexer = Lexer::make(obj_text, old.span.start.filename)
    lexer.loc = old.span.start

    let tokens = lexer.lex()
    let parser = Parser::make(program, ns)

    parser.tokens = tokens
    parser.curr = 0

    return parse_fn(&parser)
}

def Program::get_base_type(&this, base: BaseType, span: Span): &Type {
    let sym = .global.scope.lookup_local(base.str())
    if sym? and sym.type == TypeDef {
        return sym.u.type_def
    } else {
        .error(Error::new(span, `Internal compiler error, couldn't find base type {base}`))
        return null
    }
}

def Program::get_type_by_name(&this, name: str, span: Span): &Type {
    let sym = .global.scope.lookup_local(name)
    if sym? and sym.type == TypeDef {
        return sym.u.type_def
    } else {
        .error(Error::new(span, `Internal compiler error, couldn't find type {name}`))
        return null
    }
}

struct CachedSymbols {
    fmt_string_fn: &Symbol
    mem_alloc_fn: &Symbol
    mem_allocator: &Symbol
    std_vector: &Symbol
    std_map: &Symbol
    std_result: &Symbol
    std_option: &Symbol
}

def Program::iter_namespaces(&this): NSIterator {
    return NSIterator(
        stack: Vector<&Namespace>::new(),
        curr: .global
    )
}

struct NSIterator {
    stack: &Vector<&Namespace>
    curr: &Namespace
}

def NSIterator::has_value(&this): bool => .curr?

def NSIterator::next(&this) {
    for ns in .curr.namespaces.iter_values() {
        .stack.push(ns)
    }

    if .stack.is_empty() {
        .curr = null
        .stack.free()
    } else {
        .curr = .stack.pop()
    }
}

def NSIterator::cur(&this): &Namespace => .curr