//* Contains Symbol/Scope and related types

import std::mem
import std::span::{ Span, Location }
import std::map::Map
import std::vector::Vector

import @ast::nodes::{ AST, Structure, Enum, Variable, Function }
import @ast::nodes::{ ValueEnum, ValueEnumVariant }
import @ast::program::Namespace
import @types::Type

enum SymbolType {
    Function
    Structure
    Enum
    EnumVariant
    TypeDef
    Namespace
    Variable
    Constant

    ValueEnum
    ValueEnumVariant
    ValueEnumField
}

struct TemplateInstance {
    args: &Vector<&Type>
    parent: &Symbol
    resolved: &Symbol
}

def TemplateInstance::new(args: &Vector<&Type>, parent: &Symbol, resolved: &Symbol): &TemplateInstance {
    let instance = mem::alloc<TemplateInstance>()
    instance.args = args
    instance.parent = parent
    instance.resolved = resolved
    return instance
}

def TemplateInstance::matches(&this, other: &Vector<&Type>): bool {
    assert other.size == .args.size
    for let i = 0; i < other.size; i++ {
        let a = .args.at(i)
        let b = other.at(i)
        if not a.eq(b, strict: true) return false
    }
    return true
}

struct Template {
    params: &Vector<&Symbol>
    instances: &Vector<&TemplateInstance>
}

def Template::new(params: &Vector<&Symbol>): &Template {
    let templ = mem::alloc<Template>()
    templ.params = params
    templ.instances = Vector<&TemplateInstance>::new()
    return templ
}

enum ReferenceType {
    //! Normal reference, explicitly specifying the symbol
    Normal
    //! Symbol invoked as part of an operator overload
    OperatorOverload
    //! `this` symbol when used with the `.foo` shorthand
    DotShorthand
}

struct Reference {
    type: ReferenceType
    span: Span
}

struct ValueEnumField {
    variant: &ValueEnumVariant
    idx: u32
}

union SymbolUnion {
    func: &Function
    struc: &Structure
    enum_: &Enum
    ns: &Namespace
    type_def: &Type
    var: &Variable
    venom: &ValueEnum
    venom_var: &ValueEnumVariant
    venom_field: ValueEnumField
}

struct Symbol {
    //* Name in the scope it was defined in
    name: str
    //* Fully qualified display name, including namespaces
    display: str
    //* Name used in the output C code (except extern)
    full_name: str

    //* Span of the declaration
    span: Span
    //* Namespace it was defined in
    ns: &Namespace

    //* Comment attached to the declaration
    comment: str
    comment_loc: Location

    //* Span of the references to this symbol (for LSP)
    references: &Vector<Reference>

    type: SymbolType
    u: SymbolUnion

    is_extern: bool
    extern_name: str

    template: &Template
}

def Symbol::out_name(&this): str {
    if .is_extern return .extern_name
    return .full_name
}

def Symbol::new(type: SymbolType, ns: &Namespace, name: str, display: str, full_name: str, span: Span): &Symbol {
    let item = mem::alloc<Symbol>()
    item.name = name
    item.display = display
    item.references = Vector<Reference>::new(capacity: 4)  // Keep the capacity low by default

    // Replace invalid characters in full_name, since we codegen it to C
    let full_name_len = full_name.len()
    for let i = 0; i < full_name_len; i++ {
        let c = full_name[i]
        if (i == 0 and c.is_digit()) or (not c.is_alnum() and c != '_') {
            full_name[i] = '_'
        }
    }
    item.full_name = full_name

    item.span = span
    item.type = type
    item.ns = ns
    return item
}

def Symbol::join_display(a: str, b: str): str => if a.len() == 0 then b else `{a}::{b}`
def Symbol::join_full_name(a: str, b: str): str => if a.len() == 0 then b else `{a}_{b}`

def Symbol::is_templated(&this): bool => .template?

def Symbol::new_with_parent(type: SymbolType, ns: &Namespace, parent: &Symbol, name: str, span: Span): &Symbol {
    let display = Symbol::join_display(parent.display, name)
    let full_name = Symbol::join_full_name(parent.full_name, name)
    return Symbol::new(type, ns, name, display, full_name, span)
}

def Symbol::update_parent(&this, parent: &Symbol) {
    .display = Symbol::join_display(parent.display, .name)
    if not .is_extern {
        .full_name = Symbol::join_full_name(parent.full_name, .name)
    }
}

def Symbol::from_local_variable(name: str, var: &Variable, span: Span): &Symbol {
    let item = Symbol::new(
        SymbolType::Variable,
        ns: null,
        name: name,
        display: name,
        full_name: name,
        span: span,
    )
    item.u.var = var
    return item
}

def Symbol::remove_alias(&this): &Symbol => match .type {
    SymbolType::TypeDef => {
        if .u.type_def.sym != this {
            return .u.type_def.sym.remove_alias()
        }
        return this
    }
    else => this
}

def Symbol::add_reference(&this, type: ReferenceType, span: Span) {
    if not this? return
    .references.push(Reference(type, span))

    if .type == TypeDef and .u.type_def.sym != this {
        .u.type_def.sym.add_reference(type, span)
        return
    }

    // Also add a reference to the original symbol if this is a template instance
    let parent_type = match .type {
        Structure => .u.struc.type
        Function => .u.func.type
        else => null
    }

    if not parent_type? return
    if not parent_type.template_instance? return
    let child_sym = parent_type.template_instance.parent
    child_sym.add_reference(type, span)
}

struct Scope {
    items: &Map<str, &Symbol>
    defers: &Vector<&AST>

    loop_count: u32
    can_yield: bool
    cur_func: &Function

    parent: &Scope
}

def Scope::new(parent: &Scope = null): &Scope {
    let scope = mem::alloc<Scope>()
    scope.items = Map<str, &Symbol>::new()
    scope.defers = Vector<&AST>::new()
    if parent? {
        scope.loop_count = parent.loop_count
        scope.can_yield = parent.can_yield
        scope.cur_func = parent.cur_func
        scope.parent = parent
    }
    return scope
}

def Scope::lookup_recursive(&this, name: str): &Symbol {
    let item = .items.get(name, null)
    if item? return item
    if .parent? then return .parent.lookup_recursive(name)
    return null
}

def Scope::lookup_local(&this, name: str): &Symbol {
    return .items.get(name, null)
}

def Scope::insert(&this, name: str, symbol: &Symbol) {
    .items.insert(name, symbol)
}

