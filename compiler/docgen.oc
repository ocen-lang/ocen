
import std::buffer::{ Buffer }
import std::json
import std::span::{ Span }
import std::value::{ Value }
import std::vector::{ Vector }

import @ast::nodes::*
import @ast::program::{ Program, Namespace }
import @ast::scopes::Symbol
import @errors::{ Error }
import @types::{ Type, BaseType }

struct DocGenerator {
    program: &Program
}

def DocGenerator::gen_enum(&this, enum_: &Enum): &Value {
    let enum_doc = Value::new(Dictionary)
    enum_doc["id"] = `{enum_.type:x}`
    enum_doc["name"] = enum_.sym.name
    if enum_.sym.comment? {
        enum_doc["description"] = enum_.sym.comment
    }
    enum_doc["kind"] = "enum"
    if enum_.sym.is_extern {
        enum_doc["extern"] = enum_.sym.out_name()
    } else {
        .gen_location(enum_doc, enum_.sym.span)
    }

    let fields_doc = Value::new(List)
    for field : enum_.fields.iter() {
        let field_doc = Value::new(Dictionary)
        field_doc["name"] = field.sym.name
        if field.sym.comment? {
            field_doc["description"] = field.sym.comment
        }
        if field.sym.is_extern {
            field_doc["extern"] = field.sym.out_name()
        }
        fields_doc.push(field_doc)
    }
    enum_doc["fields"] = fields_doc

    let methods_doc = .gen_methods(enum_.type)
    enum_doc["methods"] = methods_doc

    return enum_doc
}

def DocGenerator::gen_location(&this, obj: &Value, span: Span) {
    let start = span.start
    if start.is_valid() {
        let src_str = `{start.filename}#L{start.line}`
        obj["source"] = src_str
    }
}

def DocGenerator::gen_templated_type(&this, base: &Type, args: &Vector<&Type>): str {
    let base_name = .gen_typename_str(base)
    let buf = Buffer::make()
    buf += base_name
    buf += "<"
    let first = true
    for arg_type : args.iter() {
        if not first then buf += ", "
        first = false

        let param_name = .gen_typename_str(arg_type)
        buf += param_name
    }
    buf += ">"
    return buf.str()
}

def DocGenerator::gen_typename_str(&this, type: &Type): str {
    assert type?, "gen_typename_str called with null type"

    match type.base {
        Char | Bool | Void | I8 | I16 | I32 | I64 |
        U8 | U16 | U32 | U64 | F32 | F64 | Alias | Enum => {
            return `\{\{{type:x}\}\}`
        }
        Structure => {
            if not type.template_instance? {
                return `\{\{{type:x}\}\}`

            } else {
                let struc = type.u.struc
                let instance = type.template_instance
                let parent: &Symbol = instance.parent
                assert parent.type == Structure, "Template instance parent is not a structure"

                let parent_struc = parent.u.struc
                return .gen_templated_type(parent_struc.type, instance.args)
            }
        }
        UnresolvedTemplate => {
            let uspec = type.u.unresolved_spec
            return .gen_templated_type(uspec.base, uspec.args)
        }
        Pointer => {
            let sub = .gen_typename_str(type.u.ptr)
            sub.replace_with(`&{sub}`)
            return sub
        }
        Array => {
            let sub = .gen_typename_str(type.u.arr.elem_type)
            sub.replace_with(`[{sub}]`)
            return sub
        }
        Function => {
            let buf = Buffer::make()
            buf += "fn("
            let first = true
            for param : type.u.func.params.iter() {
                if not first then buf += ", "
                first = false

                let param_name = .gen_typename_str(param.type)
                buf <<= param_name
            }
            buf += "): "
            let return_name = .gen_typename_str(type.u.func.return_type)
            buf <<= return_name
            return buf.str()
        }
        Unresolved => {
            let node = type.u.unresolved
            match node.type {
                Identifier => {
                    return node.u.ident.name.copy()
                }
                else => {
                    .program.error(Error::new(
                        node.span, f"Unhandled node in gen_typename_str: {node.type}"
                    ))
                    return "<unknown>"
                }
            }
        }
        else => {
            .program.error(Error::new(
                type.span, f"Unhandled type in gen_typename_str: {type.base}"
            ))
            return "<unknown>"
        }
    }
}

def DocGenerator::gen_typename(&this, type: &Type): &Value {
    assert type?, "gen_typename called with null type"
    return Value::new_str(.gen_typename_str(type))
}

def DocGenerator::gen_methods(&this, type: &Type): &Value {
    assert type.can_have_methods(), "gen_methods called with type that can't have methods"

    let methods_doc = Value::new(Dictionary)
    for it : type.methods.iter() {
        let method = it.value
        methods_doc[method.sym.name] = .gen_function(method)
    }
    return methods_doc
}

def DocGenerator::gen_function(&this, func: &Function): &Value {
    let func_doc = Value::new(Dictionary)
    func_doc["id"] = `{func:x}`
    func_doc["name"] = `{func.sym.name}`

    if func.sym.comment? {
        func_doc["description"] = func.sym.comment
    }
    if func.is_method {
        func_doc["kind"] = "method"
        func_doc["parent"] = .gen_typename(func.parent_type)
    } else {
        func_doc["kind"] = "function"
    }

    if func.sym.is_extern {
        func_doc["extern"] = func.sym.out_name()
    } else {
        .gen_location(func_doc, func.span)
    }

    let return_doc = Value::new(Dictionary)
    let ret_type = func.return_type
    return_doc["type"] = .gen_typename(ret_type)

    let params_doc = Value::new(List)
    for param : func.params.iter() {
        let param_doc = Value::new(Dictionary)
        param_doc["name"] = `{param.sym.name}`
        if param.sym.comment? {
            param_doc["description"] = param.sym.comment
        }
        param_doc["type"] = .gen_typename(param.type)

        let default_value = param.default_value
        if default_value? {
            let value_str = Value::new_str(.program.get_source_text(default_value.span))
            param_doc["default_value"] = value_str
        }

        params_doc += param_doc
    }

    func_doc["params"] = params_doc
    func_doc["return"] = return_doc
    return func_doc
}

def DocGenerator::gen_struct(&this, struc: &Structure): &Value {
    let struc_doc = Value::new(Dictionary)
    struc_doc["id"] = `{struc.type:x}`
    struc_doc["name"] = `{struc.sym.name}`
    if struc.sym.comment? {
        struc_doc["description"] = struc.sym.comment
    }
    if struc.is_union {
        struc_doc["kind"] = "union"
    } else {
        struc_doc["kind"] = "struct"
    }
    struc_doc["is_templated"] = Value::new_bool(struc.sym.is_templated())

    if struc.sym.is_templated() {
        let params_doc = Value::new(List)
        for var : struc.sym.template.params.iter() {
            let name = var.sym.name
            params_doc.push(Value::new_str(name))
        }
        struc_doc["template_params"] = params_doc
    }
    if struc.sym.is_extern {
        struc_doc["extern"] = struc.sym.out_name()
    } else {
        .gen_location(struc_doc, struc.span)
    }

    let fields_doc = Value::new(List)
    for field : struc.fields.iter() {
        let field_doc = Value::new(Dictionary)
        field_doc["name"] = field.sym.name
        field_doc["type"] = .gen_typename(field.type)
        if field.sym.comment? {
            field_doc["description"] = field.sym.comment
        }
        if field.sym.is_extern {
            field_doc["extern"] = field.sym.out_name()
        }
        fields_doc.push(field_doc)
    }
    struc_doc["fields"] = fields_doc

    let methods_doc = .gen_methods(struc.type)
    struc_doc["methods"] = methods_doc

    return struc_doc
}

def DocGenerator::gen_ns(&this, ns: &Namespace): &Value {
    let ns_doc = Value::new(Dictionary)
    ns_doc["id"] = `{ns:x}`
    if ns.sym.comment? {
        ns_doc["description"] = ns.sym.comment
    }
    ns_doc["name"] = ns.sym.name
    ns_doc["kind"] = "namespace"
    if ns.is_a_file or ns.is_top_level {
        .gen_location(ns_doc, ns.span)
    }

    if not ns.enums.is_empty() {
        let enums_doc = Value::new(Dictionary)
        for enum_ : ns.enums.iter() {
            enums_doc[enum_.sym.name] = .gen_enum(enum_)
        }
        ns_doc["enums"] = enums_doc
    }

    if not ns.structs.is_empty() {
        let structs_doc = Value::new(Dictionary)
        let unions_doc = Value::new(Dictionary)
        for struc : ns.structs.iter() {
            let struct_doc = .gen_struct(struc)
            if not struc.is_union {
                structs_doc[struc.sym.name] = struct_doc
            } else {
                unions_doc[struc.sym.name] = struct_doc
            }
        }

        if not structs_doc.as_dict().is_empty() {
            ns_doc["structs"] = structs_doc
        }
        if not unions_doc.as_dict().is_empty() {
            ns_doc["unions"] = unions_doc
        }
    }

    if not ns.variables.is_empty() {
        let vars_doc = Value::new(Dictionary)
        for node : ns.variables.iter() {
            let var = node.u.var_decl.var
            let var_doc = Value::new(Dictionary)
            var_doc["id"] = `{var:x}`
            if var.sym.comment? {
                var_doc["description"] = var.sym.comment
            }
            var_doc["name"] = var.sym.name
            var_doc["kind"] = "variable"
            var_doc["type"] = .gen_typename(var.type)
            .gen_location(var_doc, var.sym.span)
            if var.sym.is_extern {
                var_doc["extern"] = var.sym.out_name()
            }
            vars_doc[var.sym.name] = var_doc
        }
        ns_doc["variables"] = vars_doc
    }

    if not ns.constants.is_empty() {
        let consts_doc = Value::new(Dictionary)
        for node : ns.constants.iter() {
            let var = node.u.var_decl.var
            let const_doc = Value::new(Dictionary)
            const_doc["id"] = `{var:x}`
            if var.sym.comment? {
                const_doc["description"] = var.sym.comment
            }
            const_doc["name"] = var.sym.name
            const_doc["kind"] = "constant"
            const_doc["type"] = .gen_typename(var.type)
            .gen_location(const_doc, var.sym.span)
            if var.sym.is_extern {
                const_doc["extern"] = var.sym.out_name()
            }
            consts_doc[var.sym.name] = const_doc
        }
        ns_doc["constants"] = consts_doc
    }

    if not ns.functions.is_empty() {
        let funcs_doc = Value::new(Dictionary)
        for func : ns.functions.iter() {
            if func.is_method {
                continue
            }
            let func_doc = .gen_function(func)
            funcs_doc[func.sym.name] = func_doc
        }
        ns_doc["functions"] = funcs_doc
    }

    if not ns.typedefs.is_empty() {
        let typedefs_doc = Value::new(Dictionary)
        for it : ns.typedefs.iter() {
            let typedef_doc = Value::new(Dictionary)
            typedef_doc["kind"] = "typedef"
            typedef_doc["name"] = it.key.copy()
            typedef_doc["type"] = .gen_typename(it.value)
            typedefs_doc[it.key] = typedef_doc
        }
        ns_doc["typedefs"] = typedefs_doc
    }

    if not ns.namespaces.is_empty() {
        let namespaces_doc = Value::new(Dictionary)
        for it : ns.namespaces.iter() {
            let ns_doc = .gen_ns(it.value)
            namespaces_doc[it.key] = ns_doc
        }
        ns_doc["namespaces"] = namespaces_doc
    }

    return ns_doc
}

def DocGenerator::gen_builtin(&this, type: &Type): &Value {
    let type_doc = Value::new(Dictionary)
    type_doc["id"] = `{type:x}`
    type_doc["name"] = `{type.sym.name}`
    type_doc["description"] = `Built-in type {type.sym.name}`
    type_doc["kind"] = "builtin"

    let methods_doc = .gen_methods(type)
    type_doc["methods"] = methods_doc

    return type_doc
}

def DocGenerator::gen_builtins(&this, program: &Program): &Value {
    let builtins_doc = Value::new(Dictionary)
    for let i = 0; i < BaseType::NUM_BASE_TYPES as u32; i += 1 {
        let type = program.get_base_type(i as BaseType, Span::default())
        builtins_doc[type.name] = .gen_builtin(type)
    }

    let str_type = program.get_type_by_name("str", Span::default())
    builtins_doc["str"] = .gen_builtin(str_type)

    let untyped_ptr_type = program.get_type_by_name("untyped_ptr", Span::default())
    builtins_doc["untyped_ptr"] = .gen_builtin(untyped_ptr_type)

    return builtins_doc
}

def generate_doc_json(program: &Program, json_path: str) {
    let docs_generator = DocGenerator(program)

    let docs = docs_generator.gen_ns(program.global)
    let builtins = docs_generator.gen_builtins(program)
    docs["builtins"] = builtins

    let container = Value::new(Dictionary)
    container["ocen"] = docs

    json::write_to_file(container, json_path)
}