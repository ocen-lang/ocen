//* Topologically sort symbols
//*
//* This is needed because in C structs/enums/etc need to be declared before they are used
//* in others. The ordered structs are stored in `program.ordered_syms`

import std::mem
import @passes::generic_pass::GenericPass
import std::vector::Vector
import std::set::{ Set }
import @ast::program::{ Program, Namespace }
import @ast::nodes::{ Structure, Variable }
import @ast::scopes::{ TemplateInstance, Symbol }

struct ReorderSymbols {
    o: &GenericPass
    all_syms: &Vector<&Symbol>
    done: &Set<untyped_ptr>
}

def ReorderSymbols::new(program: &Program): &ReorderSymbols {
    let pass = mem::alloc<ReorderSymbols>()
    *pass = ReorderSymbols(
        o: GenericPass::new(program),
        all_syms: Vector<&Symbol>::new(),
        done: Set<untyped_ptr>::new(),
    )
    return pass
}

def ReorderSymbols::free(&this) {
    .all_syms.free()
    .done.free()
    mem::free(.o)
    mem::free(this)
}

def ReorderSymbols::collect_sym(&this, sym: &Symbol) {
    if sym? and not sym.is_dead {
        .all_syms += sym
    }
}

// Collects all structs we have in the namespace into the vector
def ReorderSymbols::collect_all_symbols_ns(&this, ns: &Namespace) {
    for struc in ns.structs.iter() {
        if struc.sym.is_templated() {
            for instance in struc.sym.template.instances.iter() {
                let sym = instance.resolved
                assert sym.type == Structure
                .collect_sym(sym)
            }

        } else {
            .collect_sym(struc.sym)
        }
    }
    for enom in ns.enums.iter() {
        if enom.sym.is_templated() {
            for instance in enom.sym.template.instances.iter() {
                let sym = instance.resolved
                assert sym.type == Enum
                .collect_sym(sym)
            }

        } else {
            .collect_sym(enom.sym)
        }
    }
    for child in ns.namespaces.iter_values() {
        .collect_all_symbols_ns(child)
    }
}

def ReorderSymbols::collect_all_symbols(&this) {
    .collect_all_symbols_ns(.o.program.global)
    for cty in .o.program.closure_types.iter() {
        .collect_sym(cty.sym)
    }
    for closure in .o.program.closures.iter() {
        .collect_sym(closure.sym)
    }
}

def ReorderSymbols::dfs(&this, sym: &Symbol) {
    if not sym? or .done.contains(sym) return
    .done.add(sym)

    match sym.type {
        Structure => {
            for field in sym.u.struc.fields.iter() {
                if field.type? {
                    .dfs(field.type.sym)
                }
            }
        }
        Enum => {
            for field in sym.u.enom.shared_fields.iter() {
                if field.type? {
                    .dfs(field.type.sym)
                }
            }
            for variant in sym.u.enom.variants.iter() {
                for field in variant.specific_fields.iter() {
                    if field.type? {
                        .dfs(field.type.sym)
                    }
                }
            }
        }
        Closure => {
            let func = sym.u.func
            for param in func.params.iter() {
                .dfs(param.type.sym)
            }
            .dfs(func.return_type.sym)
        }
        ClosureType => .dfs(sym.u.type_def.sym)
        else => return  // Don't care about other types
    }
    .o.program.ordered_symbols += sym
}

// This function topologically sorts the structs based on depedencies, and stores
// the result inside `program.ordered_structs`
def ReorderSymbols::reorder_structs(&this) {
    for sym in .all_syms.iter() {
        .dfs(sym)
    }
}

def ReorderSymbols::run(program: &Program) {
    let pass = ReorderSymbols::new(program)
    pass.collect_all_symbols()
    pass.reorder_structs()
    pass.free()
}