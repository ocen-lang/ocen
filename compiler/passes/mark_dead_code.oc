//* Mark Dead Code
//*
//* This pass marks all functions / structs / enums that are not used as dead, so 
//* that they can be removed and not compiled into the final binary.

import std::vector::Vector
import std::mem
import std::map::Map
import std::set::{ Set }

import @passes::generic_pass::GenericPass
import @ast::program::{ Program, Namespace }
import @ast::nodes::{ Structure, Variable, Function, AST }
import @ast::scopes::{ TemplateInstance, Symbol }
import @types::{ Type }
import @errors::{ Error }

struct MarkDeadCode {
    o: &GenericPass
    done: &Set<u64>
}

def MarkDeadCode::new(program: &Program): &MarkDeadCode {
    let pass = mem::alloc<MarkDeadCode>()
    *pass = MarkDeadCode(
        o: GenericPass::new(program),
        done: Set<u64>::new(),
    )
    return pass
}

def MarkDeadCode::free(&this) {
    .done.free()
    mem::free(.o)
    mem::free(this)
}

def MarkDeadCode::mark_sym(&this, sym: &Symbol) {
    if not sym? return

    match sym.type {
        Function => .mark_function(sym.u.func)
        Structure => .mark_struct(sym.u.struc)
        Constant | Variable => .mark_type(sym.u.var.type)
        else => {}
    }
}

def MarkDeadCode::mark_function(&this, f: &Function) {
    if not f? or .done.contains(f as u64) return

    .done.add(f as u64)
    f.is_dead = false
    .mark(f.body)

    for param : f.params.iter() {
        .mark_type(param.type)
    }
}

def MarkDeadCode::mark_type(&this, typ: &Type) {
    if not typ? return
    match typ.base {
        Pointer | Alias => .mark_type(typ.u.ptr)
        Array => .mark_type(typ.u.arr.elem_type)
        Function => {
            let ft = typ.u.func
            .mark_type(ft.return_type)
            for param : ft.params.iter() {
                .mark_type(param.type)
            }
        }
        else => {
            .mark_sym(typ.sym)
            if typ.template_instance? {
                for arg : typ.template_instance.args.iter() {
                    .mark_type(arg)
                }
            }
        }
    }
}

def MarkDeadCode::mark_struct(&this, s: &Structure) {
    if not s? or .done.contains(s as u64) return
    .done.add(s as u64)

    s.is_dead = false
    for field : s.fields.iter() {
        .mark_type(field.type)
    }
}

def MarkDeadCode::mark(&this, node: &AST) {
    if not node? return
    .mark_sym(node.resolved_symbol)

    match node.type {
        Import | Break | Continue | IntLiteral | FloatLiteral |
        BoolLiteral | StringLiteral | CharLiteral | Null => {}

        Yield | Defer | CreateNew => .mark(node.u.child)
        Return => .mark(node.u.ret.expr)
        UnaryOp => .mark(node.u.unary.expr)

        SizeOf => .mark_type(node.u.size_of_type)
        VarDeclaration => {
            .mark(node.u.var_decl.init)
            .mark_type(node.u.var_decl.var.type)
        }
        Block => {
            for statement : node.u.block.statements.iter() {
                .mark(statement)
            }
        }
        If => {
            for branch in node.u.if_stmt.branches.iter() {
                .mark(branch.cond)
                .mark(branch.body)
            }
            .mark(node.u.if_stmt.els)
        }
        Match => {
            .mark(node.u.match_stmt.expr)
            for c : node.u.match_stmt.cases.iter() {
                for cond in c.conds.iter() {
                    .mark(cond.expr)
                    if cond.cmp_fn? {
                        .mark_function(cond.cmp_fn)
                    }
                }
                .mark(c.body)
            }
            .mark(node.u.match_stmt.defolt)
        }
        While | For => {
            .mark(node.u.loop.init)
            .mark(node.u.loop.cond)
            .mark(node.u.loop.step)
            .mark(node.u.loop.body)
        }
        Assert => {
            .mark(node.u.assertion.expr)
            .mark(node.u.assertion.msg)
        }
        FormatStringLiteral => {
            .mark_sym(.o.program.cached_symbols.fmt_string_fn)
            for expr : node.u.fmt_str.exprs.iter() {
                .mark(expr)
            }
        }
        Member => {
            .mark(node.u.member.lhs)
        }
        Identifier | Specialization | OverloadedOperator => {}
        NSLookup => .mark(node.u.lookup.lhs)
        Call => {
            .mark(node.u.call.callee)
            for arg : node.u.call.args.iter() {
                .mark(arg.expr)
            }
        }
        BinaryOp => {
            .mark(node.u.binary.lhs)
            .mark(node.u.binary.rhs)
        }
        Cast => {
            .mark(node.u.cast.lhs)
            .mark_type(node.u.cast.to)
        }

        ArrayLiteral => {
            for expr : node.u.array_literal.elements.iter() {
                .mark(expr)
            }
        }

        else => .o.error(Error::new(node.span, `Unhandled expression type in MarkDeadCode: {node.type}`))
    }
}

def MarkDeadCode::find_main_function(&this, program: &Program): &Function {
    for ns : program.iter_namespaces() {
        for f : ns.functions.iter() {
            if f.sym.full_name.eq("main") {
                return f
            }
        }
    }
    return null
}

def MarkDeadCode::mark_sym_as_dead_by_default(&this, sym: &Symbol) {
    if not sym? return

    match sym.type {
        Function => sym.u.func.is_dead = true
        Structure => sym.u.struc.is_dead = true
        Enum => sym.u.enom.is_dead = true
        else => {}
    }

    if sym.template? {
        for instance : sym.template.instances.iter() {
            .mark_sym_as_dead_by_default(instance.resolved)
        }
    }
}

def MarkDeadCode::run(program: &Program) {
    let pass = MarkDeadCode::new(program)
    defer pass.free()

    let main = pass.find_main_function(program)
    if not main? {
        println("[+] No main function found, not marking any functions as dead.")
        return
    }

    if program.keep_all_code {
        return
    }

    // Mark all functions as dead by default
    for ns : program.iter_namespaces() {
        for f : ns.functions.iter() {
            pass.mark_sym_as_dead_by_default(f.sym)
        }
        for s : ns.structs.iter() {
            pass.mark_sym_as_dead_by_default(s.sym)
        }
        for e : ns.enums.iter() {
            pass.mark_sym_as_dead_by_default(e.sym)
        }
    }

    // For constants and variables, mark their types as used
    for ns : program.iter_namespaces() {
        for node : ns.constants.iter() {
            let var = node.u.var_decl.var
            pass.mark_type(var.type)
            if node.u.var_decl.init? {
                pass.mark(node.u.var_decl.init)
            }
        }
        for node : ns.variables.iter() {
            let var = node.u.var_decl.var
            pass.mark_type(var.type)
            if node.u.var_decl.init? {
                pass.mark(node.u.var_decl.init)
            }
        }
    }

    pass.mark_function(main)
}