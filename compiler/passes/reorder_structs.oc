//* Topologically sort structs
//*
//* This is needed because in C structs need to be declared before they are used
//* in other structs. The ordered structs are stored in `program.ordered_structs`

import std::mem
import @passes::generic_pass::GenericPass
import std::vector::Vector
import std::set::{ Set }
import @ast::program::{ Program, Namespace }
import @ast::nodes::{ Structure, Variable }
import @ast::scopes::{ TemplateInstance }

struct ReorderStructs {
    o: &GenericPass
    all_structs: &Vector<&Structure>
    done: &Set<str>
}

def ReorderStructs::new(program: &Program): &ReorderStructs {
    let pass = mem::alloc<ReorderStructs>()
    *pass = ReorderStructs(
        o: GenericPass::new(program),
        all_structs: Vector<&Structure>::new(),
        done: Set<str>::new(),
    )
    return pass
}

def ReorderStructs::free(&this) {
    .all_structs.free()
    .done.free()
    mem::free(.o)
    mem::free(this)
}

// Collects all structs we have in the namespace into the vector
def ReorderStructs::collect_all_structs(&this, ns: &Namespace) {
    for struc : ns.structs.iter() {
        if struc.sym.is_templated() {
            for instance : struc.sym.template.instances.iter() {
                let sym = instance.resolved
                assert sym.type == Structure
                .all_structs += sym.u.struc
            }

        } else {
            .all_structs += struc
        }
    }
    for child : ns.namespaces.iter_values() {
        .collect_all_structs(child)
    }
}

def ReorderStructs::dfs(&this, struc: &Structure) {
    if struc.sym.full_name in .done return
    .done += struc.sym.full_name

    for field : struc.fields.iter() {
        if field.type? and field.type.base == Structure {
            let other = field.type.u.struc
            .dfs(other)
        }
    }
    .o.program.ordered_structs += struc
}

// This function topologically sorts the structs based on depedencies, and stores
// the result inside `program.ordered_structs`
def ReorderStructs::reorder_structs(&this) {
    for struc : .all_structs.iter() {
        .dfs(struc)
    }
}

def ReorderStructs::run(program: &Program) {
    let pass = ReorderStructs::new(program)
    pass.collect_all_structs(program.global)
    pass.reorder_structs()
    pass.free()
}