//! Image struct / IO utilities (PPM)

import std::mem
import std::buffer::Buffer
import std::fs

[formatting "Color(%u, %u, %u)" "$.r, $.g, $.b"]
//* A type that holds an Image in 24-bit RGB format
struct Color {
    r: u8
    g: u8
    b: u8
}

[formatting "Image(%u x %u)" "$.width, $.height"]
struct Image {
    width: u32
    height: u32
    data: &Color
}

def Image::new(width: u32, height: u32): &Image {
    let img = mem::alloc<Image>()
    *img = Image(width, height, mem::alloc<Color>(width * height))
    return img
}

def Image::free(&this) {
    mem::free(.data)
    mem::free(this)
}

def Image::get(&this, x: u32, y: u32): Color => .data[y * .width + x]
def Image::set(&this, x: u32, y: u32, col: Color) { .data[y * .width + x] = col }

def Image::save(&this, filename: str) {
    import .ppm
    import .qoi
    import .png

    let buffer = Buffer::make()
    if filename.ends_with(".ppm") {
        ppm::encode(this, &buffer)
    } else if filename.ends_with(".qoi") {
        qoi::encode(this, &buffer)
    } else if filename.ends_with(".png") {
        png::encode(this, &buffer)
    } else {
        assert false, `Unknown file format for image: ${filename}`
    }

    fs::write_file(filename, buffer)
    buffer.free()
}

def Image::load(filename: str): &Image {
    import .ppm
    import .qoi
    import .png

    let data = fs::read_file(filename)

    let image = null as &Image
    if filename.ends_with(".ppm") {
        image = ppm::decode(&data)
    } else if filename.ends_with(".qoi") {
        image = qoi::decode(&data)
    } else if filename.ends_with(".png") {
        image = png::decode(&data)
    } else {
        assert false, `Unknown file format for image: ${filename}`
    }

    assert image?, `Failed to load image: ${filename}`
    data.free()
    return image
}

def interp(t: f32, a: f32, b: f32): f32 {
    return a * (1.0 - t) + b * t
}

def bilinear_interp(t0: f32, t1: f32, a: f32, b: f32, c: f32, d: f32):f32 {
    return interp(t1, interp(t0, a, b), interp(t0, c, d))
}

def Image::get_interpolated(&this, xf: f32, yf: f32): Color {
    assert 0.0 <= xf < (.width as f32)
    assert 0.0 <= yf < (.height as f32)

    let x_fl = xf as u32
    let y_fl = yf as u32

    let x_cl = if xf != (x_fl as f32) then x_fl + 1 else x_fl
    let y_cl = if yf != (y_fl as f32) then y_fl + 1 else y_fl

    let a = .get(x_fl, y_fl)
    let b = .get(x_cl, y_fl)
    let c = .get(x_fl, y_cl)
    let d = .get(x_cl, y_cl)

    let x_ratio = xf - (x_fl as f32)
    let y_ratio = yf - (y_fl as f32)

    let col = Color(
        bilinear_interp(x_ratio, y_ratio, a.r as f32, b.r as f32, c.r as f32, d.r as f32) as u8,
        bilinear_interp(x_ratio, y_ratio, a.g as f32, b.g as f32, c.g as f32, d.g as f32) as u8,
        bilinear_interp(x_ratio, y_ratio, a.b as f32, b.b as f32, c.b as f32, d.b as f32) as u8,
    )
    return col
}

def Image::resize(&this, new_width: u32, new_height: u32): &Image {
    let resized = Image::new(new_width, new_height)
    let x_ratio = .width as f32 / new_width as f32
    let y_ratio = .height as f32 / new_height as f32
    for let ny = 0; ny < new_height; ++ny {
        for let nx = 0; nx < new_width; ++nx {
            let x = (nx as f32 * x_ratio)
            let y = (ny as f32 * y_ratio)
            resized.set(nx, ny, .get_interpolated(x, y))
        }
    }
    return resized
}

enum RotationDirection {
    Clockwise
    CounterClockwise
}

def Image::rotate(&this, dir: RotationDirection = RotationDirection::Clockwise): &Image {
    let rotated = Image::new(.height, .width)
    for let y = 0; y < .height; ++y {
        for let x = 0; x < .width; ++x {
            let col = if dir == RotationDirection::CounterClockwise {
                yield .get(.width - x - 1, y)
            } else {
                yield .get(x, .height - y - 1)
            }
            rotated.set(y, x, col)
        }
    }
    return rotated
}

def Image::clear(&this) {
    std::libc::memset(.data, 0, .width * .height * sizeof(Color))
}

def Image::copy(&this): &Image {
    let copy = Image::new(.width, .height)
    std::libc::memcpy(copy.data, .data, .width * .height * sizeof(Color))
    return copy
}