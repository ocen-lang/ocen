//* A growable buffer / string-builder type
//*
//* The buffer is a growable array of bytes, with a null terminator at the end.
//* It can be used as a string builder, or as a string itself. It is also useful
//* to hold binary data.

import std::libc::{ realloc, calloc, memcpy, free, exit }
import std::sv::{ SV }

// Maybe abstract away the resizing with a `Buffer` type, 
// and share this with `OldVector`
[formatting "%.*s" "$.size, $.data"]
struct Buffer {
    data: &u8
    size: u32
    capacity: u32
}

def Buffer::make(capacity: u32 = 16): Buffer {
    return Buffer(
        data: calloc(capacity, 1),
        size: 0,
        capacity
    )
}

def Buffer::from_str(s: str): Buffer {
    return Buffer(
        data: s as &u8,
        size: s.len() as u32,
        capacity: s.len() as u32,
    )
}

def Buffer::from_sv(sv: SV): Buffer {
    let data = std::new<u8>(sv.len + 1)
    memcpy(data, sv.data, sv.len)
    data[sv.len] = '\0' as u8
    return Buffer(
        data: data,
        size: sv.len,
        capacity: sv.len,
    )
}

def Buffer::from_sized_str(s: str, size: u32): Buffer {
    return Buffer(
        data: s as &u8,
        size: size as u32,
        capacity: size as u32,
    )
}

def Buffer::resize_if_necessary(&this, new_size: u32) {
    if new_size >= .capacity {
        let new_capacity = u32::max(.capacity, new_size)
        .data = realloc(.data, new_capacity as u32) as &u8
        .capacity = new_capacity as u32
        assert .data?, "Out of memory!"
    }
}

def hex_dump(data: &u8, size: u32) {
    print("(%d bytes): ", size)
    for let i = 0; i < size; i += 1 {
        if (i % 4 == 0 and i > 0) print("_")
        print("%02x", data[i])
    }
    println("")
}

def Buffer::hex_dump(this) => hex_dump(.data, .size)

def Buffer::putb(&this, buf: &Buffer) {
    .resize_if_necessary(new_size: .size + buf.size + 1)
    memcpy(.data + .size, buf.data, buf.size)
    .data[.size + buf.size] = '\0' as u8
    .size += buf.size
}

def Buffer::putbf(&this, buf: &Buffer) {
    .putb(buf)
    buf.free()
}

def Buffer::puts(&this, s: str) {
    let len = s.len() as u32
    .resize_if_necessary(new_size: .size + len as u32 + 1) // +1 for null terminator
    memcpy(.data + .size, s, len + 1) // Copy over null terminator
    .size += len
}

def Buffer::putc(&this, c: char) {
    .resize_if_necessary(new_size: .size + 2) // +1 for null terminator
    .data[.size] = c as u8
    .size += 1
    .data[.size] = '\0' as u8
}

// Put and free the string
def Buffer::putsf(&this, s: str) {
    .puts(s)
    free(s)
}

def Buffer::put_bytes(&this, bytes: &u8, size: u32) {
    .resize_if_necessary(new_size: .size + size + 1)
    memcpy(.data + .size, bytes, size)
    .size += size
    .data[.size] = '\0' as u8
}

def Buffer::putsv(&this, sv: SV) => .put_bytes(sv.data as &u8, sv.len)

def Buffer::str(this): str => .data as str
def Buffer::new_str(this): str => (.data as str).copy()
def Buffer::sv(this): SV => SV(.data as str, .size)

def Buffer::copy(&this): Buffer {
    let new_data = calloc(.capacity, 1)
    memcpy(new_data, .data, .size)
    return Buffer(
        data: new_data,
        size: .size,
        capacity: .capacity,
    )
}

def Buffer::clear(&this) {
    .size = 0
}

def Buffer::free(&this) {
    free(.data)
}